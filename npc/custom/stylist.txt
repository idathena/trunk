//===== rAthena Script =======================================
//= Stylist
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.2
//===== Compatible With: =====================================
//= rAthena SVN
//===== Description: =========================================
//= Changes your hair style, hair color, and cloth color.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.1 Switched to 'getbattleflag', credits to Saithis. [Euphy]
//= 1.2 Fix style start at min_style, add Job_Summoner [AnnieRuru]
//============================================================

prontera,170,180,1	script	Stylist#custom_stylist	2_M_DYEINGER,{
	.@choose = select(" ~ Hair style: ~ Hair color: ~ Cloth color") - 1;
	.@part = .look[.@choose];
	if (BaseClass != Job_Summoner)
		callsub(L_styles, .@part, .minstyle[.@part], .maxstyle[.@part]);
	else
		callsub(L_styles, .@part, .doram_minstyle[.@part], .doram_maxstyle[.@part]);

L_styles:
	.@lookpart = getarg(0);
	.@minstyle = getarg(1);
	.@maxstyle = getarg(2);
	.@i = .@revert = getlook(.@lookpart);
	while (true) {
		setlook .@lookpart, .@i;
		message strcharinfo(0), "This is style #" + .@i + ".";
		if (.@i == .@maxstyle)
			.@next = .@minstyle;
		else
			.@next = .@i + 1;
		if (.@i == .@minstyle)
			.@previous = .@maxstyle;
		else
			.@previous = .@i - 1;
		.@menu$ = " ~ Next (^0055FF" + .@next + "^000000): ~ Previous (^0055FF" + .@previous + "^000000): ~ Jump to...: ~ Revert to original (^0055FF" + .@revert + "^000000)";
		switch (select(.@menu$)) {
		case 1:
			.@i = .@next;
			break;
		case 2:
			.@i = .@previous;
			break;
		case 3:
			message strcharinfo(0), "Choose a style between " + .@minstyle + " - " + .@maxstyle + ".";
			input .@i, .@minstyle, .@maxstyle;
			break;
		case 4:
			.@i = .@revert;
			break;
		}
	}
	end;

OnInit:
	setarray .look[0], LOOK_HAIR, LOOK_HAIR_COLOR, LOOK_CLOTHES_COLOR;
	.minstyle[LOOK_HAIR] = getbattleflag("min_hair_style");
	.maxstyle[LOOK_HAIR] = getbattleflag("max_hair_style");
	.minstyle[LOOK_HAIR_COLOR] = getbattleflag("min_hair_color");
	.maxstyle[LOOK_HAIR_COLOR] = getbattleflag("max_hair_color");
	.minstyle[LOOK_CLOTHES_COLOR] = getbattleflag("min_cloth_color");
	.maxstyle[LOOK_CLOTHES_COLOR] = getbattleflag("max_cloth_color");
	.doram_minstyle[LOOK_HAIR] = getbattleflag("min_doram_hair_style");
	.doram_maxstyle[LOOK_HAIR] = getbattleflag("max_doram_hair_style");
	.doram_minstyle[LOOK_HAIR_COLOR] = getbattleflag("min_doram_hair_color");
	.doram_maxstyle[LOOK_HAIR_COLOR] = getbattleflag("max_doram_hair_color");
	.doram_minstyle[LOOK_CLOTHES_COLOR] = getbattleflag("min_doram_cloth_color");
	.doram_maxstyle[LOOK_CLOTHES_COLOR] = getbattleflag("max_doram_cloth_color");
	end;
}
